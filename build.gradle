// more-commons

plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'eclipse'
    id 'idea'

    // keep dependencies up-to-date!
    alias libs.plugins.versions

    // to ensure clean code
    alias libs.plugins.errorprone

    // for deployment to Maven Central via Central Portal
    alias libs.plugins.maven.publish
}

group   = providers.gradleProperty('GROUP').get()
version = providers.gradleProperty('VERSION_NAME').get()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

apply from: "core.gradle"

wrapper {
    gradleVersion = '9.3.1'
}

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()

    pom {
        name = "More Commons"
        description = "Small utility classes useful in a variety of projects. Don't reinvent the wheel. Stay small."
        url = "https://github.com/arakelian/more-commons"

        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id = 'arakelian'
                name = 'Greg Arakelian'
                email = 'greg@arakelian.com'
            }
        }

        scm {
            connection = 'scm:git:https://github.com/arakelian/more-commons.git'
            developerConnection = 'scm:git:git@github.com:arakelian/more-commons.git'
            url = 'https://github.com/arakelian/more-commons.git'
        }
    }
}

dependencies {
    annotationProcessor libs.immutables.value
    testAnnotationProcessor libs.immutables.value
    compileOnly libs.immutables.value

    // annotations
    api libs.immutables.annotations

    // configure errorprone version
    errorprone libs.errorprone.core

    api libs.guava
    api libs.commons.io
    api libs.commons.lang3

    // logging
    testImplementation libs.log4j.api
    testImplementation libs.log4j.core
    testImplementation libs.log4j.slf4j2
    testImplementation libs.slf4j.jcl
    testImplementation libs.slf4j.jul
    api                libs.slf4j.api

    // for test fixtures
    testFixturesApi libs.commons.lang3
    testFixturesApi libs.junit.jupiter

    // for unit testing
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.platform.launcher
}
