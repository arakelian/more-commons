// more-commons

buildscript {
  repositories {
    mavenLocal()
  }
  dependencies {
    classpath "com.arakelian:gradle-multitool:2.0.4-SNAPSHOT"
  }
}
  
plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'eclipse'
    id 'idea'

    // keep dependencies up-to-date!
    id 'com.github.ben-manes.versions' version '0.21.0'

    // useful for creating immutable java beans
    id 'org.inferred.processors' version '2.2.0'

    // to ensure clean code
    id "net.ltgt.errorprone" version "0.7.1"

    // for deployment to Maven Central
    id "io.codearte.nexus-staging" version "0.20.0"

    // for building fat jar
    id "com.github.johnrengelman.shadow" version "5.0.0"

    // useful utilities
    //id "com.arakelian.multitool" version "2.0.3"
}

apply plugin: "com.arakelian.multitool"

group   = 'com.arakelian'

wrapper { 
    gradleVersion = '5.3' 
}

multitool {
    relocates += [
        'com.google.common'      : 'repackaged.com.arakelian.core.${prefix}com.google.common',
        'com.google.thirdparty'  : 'repackaged.com.arakelian.core.${prefix}com.google.thirdparty',
        'javax.annotation'       : 'repackaged.com.arakelian.core.${prefix}javax.annotation',
        'org.apache.commons'     : 'repackaged.com.arakelian.core.${prefix}org.apache.commons',
        'org.objectweb.asm'      : 'repackaged.com.arakelian.core.${prefix}org.objectweb.asm',
    ]
    
    proguardConfiguration {
        keep includedescriptorclasses:true, 'class com.arakelian.** { *; }'
        keep includedescriptorclasses:true, 'class **.com.google.common.collect.Lists { *; }'
        keep includedescriptorclasses:true, 'class **.com.google.common.io.Closer { *; }'
        
        dontnote '**.org.apache.commons.lang3.ObjectUtils'
    }
}

dependencies {
    processor 'org.immutables:value:2.7.5'

    // annotations
    compile 'org.immutables:value-annotations:2.7.5'
    
    // configure errorprone version
    errorprone 'com.google.errorprone:error_prone_core:2.3.3'
    
    shadow      'com.google.guava:guava:27.1-jre'
    shadow      'commons-io:commons-io:2.6'
    shadow      'org.apache.commons:commons-lang3:3.8.1'
    shadow      'org.ow2.asm:asm:7.1' // ClassScanner

    // logging
    testCompile 'org.apache.logging.log4j:log4j-api:2.9.1'
    testCompile 'org.apache.logging.log4j:log4j-core:2.9.1'
    testCompile 'org.apache.logging.log4j:log4j-slf4j-impl:2.9.1'
    testCompile 'org.slf4j:jcl-over-slf4j:1.7.25'
    testCompile 'org.slf4j:jul-to-slf4j:1.7.25'
    compile     'org.slf4j:slf4j-api:1.7.25'

    // for unit testing
    testCompile 'junit:junit:4.12'

    // we must mark these packages as provided because we want to export some test classes that require them,
    // and they need to be on the 'compile' classpath
    provided 'junit:junit:4.12'
}
